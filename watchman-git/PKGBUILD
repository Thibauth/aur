# Maintainer: Thibaut Horel <thibaut dot horel at gmail dot com>
pkgname=watchman-git
pkgver=2.9.8.r32.g20dbfd4
pkgrel=1
pkgdesc='watches files and takes action when they change'
arch=(i686 x86_64)
url=https://facebook.github.io/watchman/
license=('Apache')
depends=(pcre)
makedepends=(git python)
optdepends=('python: python bindings')
provides=('watchman')
conflicts=('watchman')
source=('watchman-git::git+https://github.com/facebook/watchman.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/$pkgname"
    ./autogen.sh
    ./configure --prefix=/usr --with-python
    make
}

package() {
    cd "$srcdir/$pkgname"
    make DESTDIR="$pkgdir/" install
}
